import Random;
import Game;
import Graphics2D;
import ImageResources;
import Audio;

const WIN_WIDTH = 960;
const WIN_HEIGHT = 600;

function main() {
	game = new Main();
	while (true) {
		if(game.onGameLoop())
			return;
	}
}

class Main{
	
	field window;
	field mouse;
	field pkm;
	
	field currentMenu;
	field startMenu;
	field characterMenu;
	field townScreen;
	field battleScreen;
	
	field splashMusic;
	field townMusic;
	field battleMusic;
	
	field player;
	field npcs;
	
	field beep;
	
	constructor() {
		this.window = new GameWindow("Grandma's Pokemon", 60, WIN_WIDTH, WIN_HEIGHT);
		this.mouse = new Mouse();
		this.pkm = new PokeManager();
		this.beep = Audio.SoundResource.loadFromResource("audio/MenuSelect.ogg"); 
		this.splashMusic = Audio.Music.loadFromResource("audio/SplashScreen.ogg"); 
		this.townMusic = Audio.Music.loadFromResource("audio/WanderMusic.ogg"); 
		this.battleMusic = Audio.Music.loadFromResource("audio/BattleMusic.ogg"); 
		
		this.startMenu = new StartMenu(this);
		this.currentMenu = this.startMenu;
		this.characterMenu = new CharSelect(this);
		this.splashMusic.play();
	}
	
	function onGameLoop() {
		if(this.onEvent())
			return true;
			
		this.currentMenu.onGameLoop();		

		this.window.clockTick();
		return false;
	}
	
	function gotoCharSelect() {
		this.beep.play();
		this.currentMenu = this.characterMenu;
	}
	
	function gotoTown() {
		// this.battleMusic.stop();
		this.townMusic.play();
		this.currentMenu = this.townScreen;
	}
	
	function gotoBattle(p1, p2) {
		// this.townMusic.stop();
		this.battleMusic.play();
		this.battleScreen = new Battle(p1, p2, this);
		this.currentMenu = this.battleScreen;
	}
	
	function setPlayer(id) {
		len = this.characterMenu.charTextures.length - 1;
		this.npcs = [null] * len;
		for(i = 0; i < len + 1; i++) {
			if(i < id)
				this.npcs[i] = new Person(this.characterMenu.charTextures[i], this.pkm);
			else if(i > id)
				this.npcs[i - 1] = new Person(this.characterMenu.charTextures[i], this.pkm);
		}
		this.player = new Player(this.characterMenu.charTextures[id]);
		this.player.addPokemon(this.pkm.getPokemon(Random.randomInt(0, 4)));
		this.player.addPokemon(this.pkm.getPokemon(Random.randomInt(0, 4)));
		this.player.addPokemon(this.pkm.getPokemon(Random.randomInt(0, 4)));
		this.player.addPokemon(this.pkm.getPokemon(Random.randomInt(0, 4)));
		this.townScreen = new Town(this);
		this.beep.play();
		this.gotoTown();
	}
	
	function onEvent() {
		for (event : this.window.pumpEvents()) {
			switch (event.type) {
				case EventType.QUIT:
					return true;
				
				case EventType.MOUSE_MOVE:
					this.mouse.x = event.x;
					this.mouse.y = event.y;
					break;
				
				case EventType.MOUSE_LEFT_DOWN:
				case EventType.MOUSE_LEFT_UP:
					this.mouse.isDown = event.down;
					break;
					
				default:
					break;
			}
		}
		
		return false;
	}
}
